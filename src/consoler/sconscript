
# vim: ft=python

Import('env')

consoler = env.Library('consoler.c')
env.Object('consoler_prof.o', 'consoler.c',
    CFLAGS = ['-pg', '-fprofile-arcs', '-ftest-coverage'],
    LINKFLAGS = ['-pg', '-fprofile-arcs', '-ftest-coverage'])
env.Default(env.Library('consoler_prof', 'consoler_prof.o',
    CFLAGS = ['-pg', '-fprofile-arcs', '-ftest-coverage'],
    LINKFLAGS = ['-pg', '-fprofile-arcs', '-ftest-coverage'])
    )
env['CPPPATH'].append(Dir('.'))
env['LIBPATH'].append(Dir('.'))

consolertest = env.Program('consolertest', 'consolertest.c', LIBS=['consoler'])
Default(env.Command('consolertest.out', consolertest, "$SOURCES > $TARGET"))

consolerpkgconfig = env.Substfile('consoler.pc', 'consoler.pc.in',
        SUBST_DICT= {"@PREFIX@": env['PREFIX'], "@VERSION@": env['VERSION']})
env.Default(consolerpkgconfig)

env['LIBRARYDOC']('consoler')

env.Install("$PREFIX/include", ['consoler.h', 'consoler_keysym.h'])
env.Install("$PREFIX/lib", consoler);
env.Install("$PREFIX/lib/pkgconfig", consolerpkgconfig)

env.Install("$RELEASE", [
    'consoler.c',
    'consoler.docl',
    'consoler.h',
    'consoler_keysym.h',
    'consoler.pc.in',
    'consolertest.c',
    'sconscript'
    ])

