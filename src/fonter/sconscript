
# vim: ft=python

Import('env')

fonter = env.Library('fonter.c')
env.Object('fonter_prof.o', 'fonter.c',
    CFLAGS = ['-pg', '-fprofile-arcs', '-ftest-coverage'],
    LINKFLAGS = ['-pg', '-fprofile-arcs', '-ftest-coverage'])
env.Default(env.Library('fonter_prof', 'fonter_prof.o',
    CFLAGS = ['-pg', '-fprofile-arcs', '-ftest-coverage'],
    LINKFLAGS = ['-pg', '-fprofile-arcs', '-ftest-coverage'])
    )
env['CPPPATH'].append(Dir('.'))
env['LIBPATH'].append(Dir('.'))

fontertest = env.Program('fontertest', 'fontertest.c',
        LIBS=['m', 'fontconfig', 'freetype', 'fonter', 'consoler'])
Default(env.Command('fontertest.out', fontertest, "$SOURCES > $TARGET"))

fonterexample = env.Program('fonterexample', 'fonterexample.c',
        LIBS=['m', 'fontconfig', 'freetype', 'fonter', 'consoler'])
Default(fonterexample)

fonterpkgconfig = env.Substfile('fonter.pc', 'fonter.pc.in',
        SUBST_DICT= {"@PREFIX@": env['PREFIX'], "@VERSION@": env['VERSION']})
env.Default(fonterpkgconfig)

env['LIBRARYDOC']('fonter')


env.Install("$PREFIX/include", 'fonter.h')
env.Install("$PREFIX/lib", fonter);
env.Install("$PREFIX/lib/pkgconfig", fonterpkgconfig);

env.Install("$RELEASE", [
    'fonter.c',
    'fonter.docl',
    'fonterexample.c',
    'fonter.h',
    'fonter.pc.in',
    'fontertest.c',
    'sconscript'
    ])

