
# vim: ft=python

Import('env')

fonter = env.Library('fonter.c')
env['CPPPATH'].append(Dir('.'))
env['LIBPATH'].append(Dir('.'))

fontertest = env.Program('fontertest', 'fontertest.c',
        LIBS=['fontconfig', 'freetype', 'fonter', 'consoler'])
Default(env.Command('fontertest.out', fontertest, "$SOURCES > $TARGET"))

fonterexample = env.Program('fonterexample', 'fonterexample.c',
        LIBS=['fontconfig', 'freetype', 'fonter', 'consoler'])
Default(fonterexample)

fonterpkgconfig = env.Substfile('fonter.pc', 'fonter.pc.in',
        SUBST_DICT= {"@PREFIX@": env['PREFIX'], "@VERSION@": env['VERSION']})
env.Default(fonterpkgconfig)

functions = [
    'FNTR_Version',
    'FNTR_Create',
    'FNTR_Free',
    'FNTR_MaxWidth',
    'FNTR_Height',
    'FNTR_LoadGlyph',
    'FNTR_GlyphWidth',
    'FNTR_GlyphLevel',
    'FNTR_DrawGlyph',
    'FNTR_DrawString',
    ]
env['LIBRARYDOC']('fonter', functions)


env.Install("$PREFIX/include", 'fonter.h')
env.Install("$PREFIX/lib", fonter);
env.Install("$PREFIX/lib/pkgconfig", fonterpkgconfig);

env.Install("$RELEASE", [
    'fonter.c',
    'fonter.docl',
    'fonterexample.c',
    'fonter.h',
    'fonter.pc.in',
    'fontertest.c',
    'sconscript'
    ])

