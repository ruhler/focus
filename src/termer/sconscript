
# vim: ft=python

Import('env')

bool_o = env.Object('bool.o', 'bool.c')

# inputtertest
inputter_o = env.Object('inputter.o', 'inputter.c')
inputtertest = env.Program('inputtertest', ['inputtertest.c', inputter_o, bool_o])
env.Default(env.Command('inputtertest.out', inputtertest, "$SOURCE > $TARGET"))

# screentest
screen_o = env.Object('screen.o', 'screen.c')
screentest = env.Program('screentest', ['screentest.c', screen_o, bool_o])
env.Default(env.Command('screentest.out', screentest, "$SOURCE > $TARGET"))

# outputtertest
outputter_o = env.Object('outputter.o', 'outputter.c')
outputtertest = env.Program('outputtertest',
        ['outputtertest.c', outputter_o, screen_o, bool_o])
env.Default(env.Command('outputtertest.out', outputtertest, "$SOURCE > $TARGET"))

# termer
env.Default(env.Program('termer',
    ['termer.c', 'ctermer.c', '../consoler.o', inputter_o, outputter_o, screen_o, bool_o],
    LIBS=['util', 'pthread', 'fontconfig', 'freetype', 'm']))

# sdltermer
env.Default(env.Program('sdltermer',
    ['termer.c', 'sdlctermer.c', inputter_o, outputter_o, screen_o, bool_o],
    LIBS=['util', 'pthread', 'fontconfig', 'freetype', 'm', 'SDL']))

